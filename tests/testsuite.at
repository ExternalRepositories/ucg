# testsuite.at for UniversalCodeGrep
#
# Copyright 2015-2016 Gary R. Van Sickle (grvs@users.sourceforge.net).
#
# This file is part of UniversalCodeGrep.
#
# UniversalCodeGrep is free software: you can redistribute it and/or modify it under the
# terms of version 3 of the GNU General Public License as published by the Free
# Software Foundation.
#
# UniversalCodeGrep is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
# PARTICULAR PURPOSE.  See the GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License along with
# UniversalCodeGrep.  If not, see <http://www.gnu.org/licenses/>.

# A basic test suite for UniversalCodeGrep.

###
### "Helper" m4 defines.
###

# Portable line counter.
# Needed because "wc -l" on FreeBSD and Mac OSX prepend spaces to the count.
# Might as well clip off the trailing newline while we're at it.
# Nothing. Is. Ever. Easy.
m4_define([LCT], [wc -l | tr -d '\t \r\n'])

# A somewhat more explicit way of skipping tests.
m4_define([AT_SKIP_TEST],[exit 77])

dnl Quadrigraphs (for reference):
dnl @S|@ = "$"
dnl @<:@ = "["

###
### Start of the test suite proper.
###
AT_INIT([UniversalCodeGrep Test Suite])
AT_COPYRIGHT([Copyright (C) 2015-2016 Gary R. Van Sickle])
AT_COLOR_TESTS

# We have one main executable to test.
AT_TESTED([ucg])

# Additional option for specifying the top-level directory of the benchmark corpus.
# Result will be in $at_arg_corpusdir, and during action-if-given in $at_optarg.
# action-if-not-given is executed if the user doesn't specify this option.
AT_ARG_OPTION_ARG([corpusdir], [AS_HELP_STRING([--corpusdir=PATH],
	[Relative path from the top-level ucg source dir to the top-level directory of the corpus to use for benchmarking.])],
	[AS_ECHO(["Using corpus directory \"$at_arg_corpusdir\"."])],
	[at_arg_corpusdir="./../"
	AS_ECHO(["Using default corpus directory \"$at_arg_corpusdir\"."])
])


###
### Start the "Sanity check" test category.
###
AT_BANNER([UniversalCodeGrep sanity checks])

m4_include([version.at])
m4_include([sanity.at])

###
### Check the CLI argument and .ucgrc file handling.
###
m4_include([arg_and_rc_handling.at])

###
### Check the search pattern modifiers (e.g. --ignore-case).
###
m4_include([search_modifiers.at])

###
### File presentation tests.
###
m4_include([file_presentation.at])

###
### Match correctness tests.
###
m4_include([match_correctness.at])

###
### Check type inclusion options.
###
m4_include([type_inclusion.at])

###
### Check directory tree traversal.
###
m4_include([dir_tree_traversal.at])

###
### Run some performance tests.
###
# Create the common perf test results file.
m4_include([performance_tests.at])

